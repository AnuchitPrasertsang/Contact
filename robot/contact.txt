*** Settings ***
Library             Selenium2Library
Suite Setup         Open Contact Page
Suite Teardown      Close All Browsers

*** Variables ***
${URL}              http://localhost:3333
${SUCCESS_MAIL}     test@example.com
${ERROR_MAIL}       wingyminov@mail.com
${SUBJECT}          Test subject
${MESSAGE}          Test message

*** Test Cases ***
Send Success
    Given Fill In Form With Success Data Test
    Then Send Button Should Be Enabled
    When Click Send Button
    Then Page Display Send Message Success
    When Click OK Button
    Then Form Display With Blank Data

Send Fail
    Given Fill In Form With Fail Data Test
    Then Send Button Should Be Enabled
    When Click Send Button
    Then Page Display Send Message Fail
    When Click Dismiss Button
    Then Form Display With Fill In Data

*** Keywords ***
Open Contact Page
    Open Browser    url=${URL}    browser=gc
    Set Window Size    1280    800

Fill In Form With Success Data Test
    Input Text    email      ${SUCCESS_MAIL}
    Fill Form For Subject And Content

Fill In Form With Fail Data Test
    Input Text    email      ${ERROR_MAIL}
    Fill Form For Subject And Content

Fill Form For Subject And Content
    Input Text    subject    ${SUBJECT}
    Press Key     message    ${MESSAGE}

Send Button Should Be Enabled
    Element Should Be Enabled    send-button

Click Send Button
    Click Button    send-button

Page Display Send Message Success
    Element Should Be Visible    overlay
    Element Should Be Visible    success-message
    Element Should Be Enabled    success-button

Page Display Send Message Fail
    Element Should Be Visible    overlay
    Element Should Be Visible    error-message
    Element Should Be Enabled    error-button

Click OK Button
    Click Button    success-button

Click Dismiss Button
    Click Button    error-button

Form Display With Blank Data
    Textfield Value Should Be    subject    ${EMPTY}
    Textarea Value Should Be     message    ${EMPTY}

Form Display With Fill In Data
    Textfield Value Should Be    subject    ${SUBJECT}
    Textarea Value Should Be     message    ${MESSAGE}